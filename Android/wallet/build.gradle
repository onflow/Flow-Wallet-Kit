plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.8.0'
    id 'maven-publish'
}

Properties properties = new Properties()
File localProps = new File(rootDir.absolutePath, "local.properties")
if (localProps.exists()) {
    properties.load(localProps.newDataInputStream())
    println "Authenticating user: " + properties.getProperty("gpr.user")
} else {
    println "local.properties not found, please create it next to build.gradle and set gpr.user and gpr.key (Create a GitHub package read only + non expiration token at https://github.com/settings/tokens)\n" +
            "Or set GITHUB_USER and GITHUB_TOKEN environment variables"
}

android {
    namespace 'com.flow.wallet'
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    
    kotlinOptions {
        jvmTarget = '11'
    }
}

repositories {
    google()
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://maven.google.com" }
    maven {
        url = uri("https://maven.pkg.github.com/trustwallet/wallet-core")
        credentials {
            username = properties.getProperty("gpr.user") as String?: System.getenv("GITHUB_USER")
            password = properties.getProperty("gpr.key") as String?: System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.8.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.security:security-crypto:1.1.0-alpha06'
    implementation('com.google.apis:google-api-services-drive:v3-rev20220815-2.0.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    
    // Flow SDK dependencies
    implementation 'com.github.onflow.flow-kmm:flow:0.0.25'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.0'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    
    // HD Wallet implementation
    implementation 'com.trustwallet:wallet-core:4.3.2'
    
    // Ktor dependencies
    implementation "io.ktor:ktor-client-core:2.3.7"
    implementation "io.ktor:ktor-client-cio:2.3.7"
    implementation "io.ktor:ktor-client-content-negotiation:2.3.7"
    implementation "io.ktor:ktor-serialization-kotlinx-json:2.3.7"
    implementation "io.ktor:ktor-client-logging:2.3.7"
    
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:5.3.1'
    testImplementation "io.ktor:ktor-client-mock:2.3.7"
    androidTestImplementation 'org.mockito:mockito-core:5.11.0'
    androidTestImplementation 'org.mockito.kotlin:mockito-kotlin:5.3.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'
    implementation "org.jetbrains.kotlin:kotlin-test:2.0.21"
    testImplementation "com.ionspin.kotlin:bignum:0.3.8"
    androidTestImplementation "com.ionspin.kotlin:bignum:0.3.10"
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = "sources"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.onflow'
                artifactId = 'flow-wallet-kit'
                version = '0.1.6'
                
                artifact sourceJar
                
                pom {
                    name = 'Flow Wallet Kit'
                    description = 'Flow blockchain wallet SDK for Android'
                    url = 'https://github.com/onflow/Flow-Wallet-Kit'
                    
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    
                    developers {
                        developer {
                            id = 'onflow'
                            name = 'Flow Team'
                            email = 'dev@onflow.org'
                        }
                    }
                    
                    scm {
                        connection = 'scm:git:git://github.com/onflow/Flow-Wallet-Kit.git'
                        developerConnection = 'scm:git:ssh://github.com:onflow/Flow-Wallet-Kit.git'
                        url = 'https://github.com/onflow/Flow-Wallet-Kit/tree/main'
                    }
                }
            }
        }
    }
}