plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.1.20'
    id 'maven-publish'
}

// Maven coordinates
group = project.findProperty("GROUP") ?: "com.github.onflow.flow-wallet-kit"
version = (project.findProperty("VERSION_NAME")
        ?: System.getenv("VERSION_NAME")
        ?: "0.0.0-SNAPSHOT")

Properties properties = new Properties()
File localProps = new File(rootDir.absolutePath, "local.properties")
if (localProps.exists()) {
    properties.load(localProps.newDataInputStream())
    println "Authenticating user: " + properties.getProperty("gpr.user")
} else {
    println "local.properties not found, please create it next to build.gradle and set gpr.user and gpr.key (Create a GitHub Packages token at https://github.com/settings/tokens)\n" +
            "Or set GPR_USER and GPR_TOKEN environment variables"
}

android {
    namespace 'com.flow.wallet'
    compileSdk 36
    buildToolsVersion '35.0.0'

    defaultConfig {
        minSdk 24
        targetSdk 36
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    ndkVersion '27.1.12297006'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    
    kotlinOptions {
        jvmTarget = '17'
    }

    // Configure variant-aware publishing to silence AGP warning
    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

repositories {
    google()
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://maven.google.com" }
    maven {
        url = uri("https://maven.pkg.github.com/trustwallet/wallet-core")
        credentials {
            username = (
                project.findProperty("gpr.user")
                    ?: System.getenv("GPR_USER")
                    ?: System.getenv("GITHUB_USER")
                    ?: properties.getProperty("gpr.user")
            ) as String
            password = (
                project.findProperty("gpr.key")
                    ?: System.getenv("GPR_TOKEN")
                    ?: System.getenv("GITHUB_TOKEN")
                    ?: properties.getProperty("gpr.key")
            ) as String
        }
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:2.1.20'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.security:security-crypto:1.1.0-alpha06'
    implementation('com.google.apis:google-api-services-drive:v3-rev20220815-2.0.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    
    // Flow SDK dependencies
    implementation 'com.github.onflow.flow-kmm:flow:0.1.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.7.3'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    
    // HD Wallet implementation
    implementation 'com.trustwallet:wallet-core:4.3.2'
    
    // Ktor dependencies
    implementation "io.ktor:ktor-client-core:2.3.7"
    implementation "io.ktor:ktor-client-cio:2.3.7"
    implementation "io.ktor:ktor-client-content-negotiation:2.3.7"
    implementation "io.ktor:ktor-serialization-kotlinx-json:2.3.7"
    implementation "io.ktor:ktor-client-logging:2.3.7"
    
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:5.3.1'
    testImplementation "io.ktor:ktor-client-mock:2.3.7"
    androidTestImplementation 'org.mockito:mockito-core:5.11.0'
    androidTestImplementation 'org.mockito.kotlin:mockito-kotlin:5.3.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'
    implementation "org.jetbrains.kotlin:kotlin-test:2.1.20"
    testImplementation "com.ionspin.kotlin:bignum:0.3.8"
    androidTestImplementation "com.ionspin.kotlin:bignum:0.3.10"
}

afterEvaluate {
    publishing {
        publications {
            // Publish the Android release variant
            release(MavenPublication) {
                from components.release
                // Optional: set an explicit artifactId
                artifactId = 'flow-wallet-android'

                pom {
                    name = 'flow-wallet-android'
                    description = 'Flow Wallet Kit Android library'
                    url = 'https://github.com/onflow/flow-wallet-kit'
                    licenses {
                        license {
                            name = 'Apache-2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }
                    scm {
                        url = 'https://github.com/onflow/flow-wallet-kit'
                        connection = 'scm:git:https://github.com/onflow/flow-wallet-kit.git'
                        developerConnection = 'scm:git:ssh://git@github.com/onflow/flow-wallet-kit.git'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/onflow/flow-wallet-kit")
                credentials {
                    // Prefer Actions built-in token for publishing to this repo
                    username = (
                        System.getenv("GITHUB_ACTOR")
                                ?: project.findProperty("gpr.user")
                                ?: System.getenv("GPR_USER")
                                ?: System.getenv("GITHUB_USER")
                                ?: properties.getProperty("gpr.user")
                    ) as String
                    password = (
                        System.getenv("GITHUB_TOKEN")
                                ?: project.findProperty("gpr.key")
                                ?: System.getenv("GPR_TOKEN")
                                ?: properties.getProperty("gpr.key")
                    ) as String
                }
            }
        }
    }
}
